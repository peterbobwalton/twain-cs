;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Non-UI Image Transfer Tests
;   Confirm that multiple MSG_ENABLEDS and MSG_DISABLEDS calls can be made in the context of
;   one MSG_OPENDS / MSG_CLOSEDS. This test focuses on image capture with no UI, verifying
;   that the Application does not have to close the driver after capturing images.
;
; Arguments
;   arg:1 - TW_IDENTITY.ProductName of scanner to test
;   arg:2 - global to set on fail
;
; Globals
;   (none)
;
; Locals
;   twidentity.productname - product name of scanner to test
;   certifyresult - global to set on fail
;   xfermech - result of getting ICAP_XFERMECH
;   sheets - count of sheets the user needs
;
; Returns
;   (none)
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Initialize stuff, load tables if not already done for us...
;
echo.titlesuite 'Non-UI Image Transfer Tests v1.0 01-Mar-2020'
setlocal twidentity.productname '${arg:1}'
setlocal certifyresult '${arg:2}'
setlocal sheets '0'
if '${get:TW_ARRAY.ItemType}' != '' goto MAIN.LOADTABLES.SKIP
run '${folder:certification}/LoadTables'
:MAIN.LOADTABLES.SKIP



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Open the scanner...
;
call CERTIFY '${folder:certification}/Opends'  '${get:twidentity.productname}' '${get:certifyresult}'
if '${get:${get:certifyresult}}' == 'fail' goto MAIN.CLOSEDS



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Prompt the user, if they have an ADF...
;
:MAIN.PROMPT
if '${get:g_skipprompts}' != '' goto MAIN.SKIP.PROMPTS
dsmentry src ds dg_control dat_capability msg_get 'CAP_FEEDERENABLED,0,0,0'
if '${ret:}' !~contains 'TRUE' goto MAIN.SKIP.PROMPTS
dsmentry src ds dg_control dat_capability msg_get 'ICAP_XFERMECH,0,0,0'
setlocal xfermech '${ret:}'
dsmentry src ds dg_control dat_capability msg_get 'ICAP_PIXELTYPE,0,0,0'
if '${ret:}' !~contains 'TWPT_BW' goto MAIN.PROMPTS.SKIP.BW
increment sheets '${get:sheets}' '${getindex:xfermech:${get:TW_ENUMERATION.NumItems}}'
:MAIN.PROMPTS.SKIP.BW
if '${ret:}' !~contains 'TWPT_GRAY' goto MAIN.PROMPTS.SKIP.GRAY
increment sheets '${get:sheets}' '${getindex:xfermech:${get:TW_ENUMERATION.NumItems}}'
:MAIN.PROMPTS.SKIP.GRAY
if '${ret:}' !~contains 'TWPT_RGB' goto MAIN.PROMPTS.SKIP.RGB
increment sheets '${get:sheets}' '${getindex:xfermech:${get:TW_ENUMERATION.NumItems}}'
:MAIN.PROMPTS.SKIP.RGB
echo.prompt
echo.prompt "Scanning will take place during this test.  There will be no"
echo.prompt "need to press any button on the TWAIN user interface to start"
echo.prompt "the scanner.  A progress indicator may appear during scanning."
echo.prompt
echo.prompt "Place ${get:sheets} sheets of paper in the scanner's feeder."
pause '(press the ENTER key when ready to continue)'
echo.prompt
:MAIN.SKIP.PROMPTS



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Get ICAP_XFERMECH...
;
setlocal name 'ICAP_XFERMECH MSG_GET'
dsmentry src ds dg_control dat_capability msg_get 'ICAP_XFERMECH,0,0,0'
if '${sts:}' != 'SUCCESS' goto CERTIFY.FAIL
setlocal xfermech '${ret:}'
echo.passfail '${get:name}' 'pass'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Do the tests...
;
call CERTIFY 'DAT_IMAGENATIVEXFER'             '${get:twidentity.productname}' '${get:certifyresult}' 'FALSE' '${folder:parent}'
call CERTIFY 'DAT_IMAGEMEMXFER'                '${get:twidentity.productname}' '${get:certifyresult}' 'FALSE' '${folder:parent}'
call CERTIFY 'DAT_IMAGEFILEXFER'               '${get:twidentity.productname}' '${get:certifyresult}' 'FALSE' '${folder:parent}' '${get:xfermech}'
call CERTIFY 'DAT_IMAGEMEMFILEXFER'            '${get:twidentity.productname}' '${get:certifyresult}' 'FALSE' '${folder:parent}' '${get:xfermech}'
:MAIN.CLOSEDS
call CERTIFY '${folder:certification}/Closeds' '${get:twidentity.productname}' '${get:certifyresult}'
return



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Function: Certify
; arg:1 - script to run
; arg:2 - TW_IDENTITY.ProductName of scanner to test
; arg:3 - global value to update on fail
; arg:4 - showui value
; arg:5 - name of test
; arg:6 - xfermech values
; returns: (none)
;
:CERTIFY
;
run '${arg:1}' '${arg:2}' '${arg:3}' '${arg:4}' '${arg:5}' '${arg:6}'
if '${ret:}' != 'pass' goto CERTIFY.FAIL
return 'pass'
;
:CERTIFY.FAIL
setglobal '${arg:3}' 'fail'
return 'fail'
